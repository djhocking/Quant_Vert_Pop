
model {

# Priors (new)
 # alpha.lam ~ dunif(-10, 10)
 # beta1.lam ~ dunif(-10, 10)
 # beta2.lam ~ dunif(-10, 10)
 # alpha.p ~ dunif(-10, 10)
 # beta.p ~ dunif(-10, 10)

alpha.lam ~ dnorm(0, 0.01)
beta1.lam ~ dnorm(0, 0.01)
beta2.lam ~ dnorm(0, 0.01)
alpha.p ~ dnorm(0, 0.01)
beta.p ~ dnorm(0, 0.01)


# Likelihood
# Biological model for true abundance
 for (i in 1:R) {
   N[i] ~ dpois(lambda[i])
   log(lambda[i]) <- alpha.lam + beta1.lam * vege[i] + beta2.lam * vege2[i]
 }

# Observation model for replicated counts
 for (i in 1:n) {
   C[i] ~ dbin(p[i], N[site.p[i]])
   lp[i] <- alpha.p + beta.p * vege.p[i]
   p[i] <- exp(lp[i])/(1+exp(lp[i]))
 }

# Derived quantities
 totalN <- sum(N[])

# Assess model fit using Chi-squared discrepancy
 for (i in 1:n) {
# Compute fit statistic for observed data
     eval[i]<-p[i]*N[site.p[i]]
     E[i] <- pow((C[i] - eval[i]),2) / (eval[i] + 0.5)
# Generate replicate data and compute fit stats for them
     C.new[i] ~ dbin(p[i], N[site.p[i]])
     E.new[i] <- pow((C.new[i] - eval[i]),2) / (eval[i] + 0.5)
 }
 fit <- sum(E[])
 fit.new <- sum(E.new[])
}

